import("//buildconfig/c++/c++.gni")
import("//buildconfig/c/c.gni")
import("//buildconfig/platform/platform.gni")
import("//buildconfig/toolchain/toolchain.gni")

config("compiler") {
  include_dirs = []
  defines = []
  cflags = []
  cflags_c = []
  cflags_cc = []
  cflags_objc = []
  cflags_objcc = []
  ldflags = []

  # monorepo
  include_dirs += [ "//" ]

  cflags_c += [ "-std=${std_c}" ]
  cflags_cc += [ "-std=${std_cxx}" ]

  cflags += [ "-O${optimization_level}" ]

  if (warning_level == 0) {
    cflags += [ "-fpermissive" ]
  } else if (warning_level == 1) {
    cflags += [ "-Wall" ]
  } else if (warning_level == 2) {
    cflags += [
      "-Wall",
      "-Wextra",
    ]
  } else if (warning_level == "max" || warning_level == 3) {
    cflags += [
      "-Wall",
      "-Wextra",
      "-Wpedantic",
    ]
  }

  if (use_explicit_symbol_exports) {
    cflags += [ "-fvisibility=hidden" ]
  }

  if (treat_warnings_as_errors) {
    cflags += [ "-Werror" ]
  }

  if (use_static_linking) {
    ldflags += [ "-static" ]
  }

  if (enable_debug_info) {
    cflags += [
      "-g",
      "-fno-omit-frame-pointer",
    ]
  }
}

config("asan") {
  ldflags = [ "-fsanitize=address" ]
  cflags = [ "-fsanitize=address" ]
}

config("tsan") {
  cflags = [ "-fsanitize=thread" ]
  ldflags = [ "-fsanitize=thread" ]
}

config("ubsan") {
  cflags = [ "-fsanitize=undefined" ]
  ldflags = [ "-fsanitize=undefined" ]
}

config("c") {
  ldflags = []
  if (use_static_libc) {
    ldflags += [ "-static-libgcc" ]
  }
}

config("cxx") {
  cflags_cc = []
  ldflags = []

  if (!use_rtti) {
    cflags_cc += [ "-fno-rtti" ]
  }

  if (!use_exceptions) {
    cflags_cc += [ "-fno-exceptions" ]
  }

  if (use_static_libstdcxx) {
    ldflags += [ "-static-libstdcxx" ]
  }
}

config("executable") {
  ldflags = []
  cflags = [ "-fPIE" ]

  if (is_linux) {
    # TODO, add a var to control.
    ldflags += [
      # ld.so understands the string $ORIGIN (or equivalently ${ORIGIN}) in an rpath
      # specification (DT_RPATH or DT_RUNPATH) to mean the directory containing the
      # application executable.
      "-Wl,-rpath=\$ORIGIN",
      "-Wl,-rpath-link=\$ORIGIN",
    ]
  }
}

config("shared_library") {
  cflags = [ "-fPIC" ]
}

config("static_libaray") {
  cflags = [ "-fPIC" ]
}
